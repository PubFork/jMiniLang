var g_author = func ["标准库的作者：bajdcc"] ~() -> "bajdcc";
export "g_author";
var g_println = func ~() {
    var c = g_args_count();
    if (c == 0) {
        g_print(g_endl);
        return;
    }
    for (var i = 0; i < c; i++) {
        g_print(g_args_index(i));
        g_print(g_endl);
    }
};
export "g_println";
var g_range = yield ~(a, b) {
    for (var i = a; i <= b; i++) {
        yield i;
    }
};
export "g_range";
var g_range_foreach = func ~(a, b, c) {
    foreach (var i : g_range(a, b)) {
        c(i);
    }
};
export "g_range_foreach";
var g_range_any = func ~(a, b, c) {
    foreach (var i : g_range(a, b)) {
        if (c(i)) {
            return true;
        }
    }
    return false;
};
export "g_range_any";
