// Net
import "sys.base";
import "sys.list";
import "sys.func";
import "sys.proc";
import "sys.task";
import "sys.string";
import "sys.ui";
import "module.lisp";


// --------------------------------------------------------
// SERVER
var shutdown_server = func ~(out, signal, port, log) {
    g_net_msg_shutdown_server();
    g_printn("Shutting down msg server, port: " + port);
    if (log) { g_write_pipe_n(out, "Server stopped!"); }
    while (true) {
        var s = g_net_msg_get_server_status();
        if (s == 3) {
            g_write_pipe_n(out, g_net_msg_get_error());
        }
        if (s == 0) {
            break;
        }
    }
};

var msg_receive = func ~(out, signal, port, log) {
    if (log) { g_write_pipe_n(out, "Server listening..."); }
    while (g_query_share(signal)) {
        var s = g_net_msg_get_server_msg();
        if (g_is_null(s)) {
            g_task_sleep_signal(1, signal);
            continue;
        }
        g_printdn("Received msg: " + s);
        var obj = g_net_parse_json(s);
        if (g_is_null(obj)) {
            g_task_sleep_signal(1, signal);
            continue;
        }
        var type = obj["type"];
        var addr = obj["addr"];
        var content = obj["content"];
        var msg = "[" + type + "] " + addr + ": " + content;
        if (log) { g_write_pipe_n(out, "[" + type + "] " + addr + ": " + content); }
        else if (type == "MSG ") { g_write_pipe_n(out, content); }
        g_printdn("Received msg: " + msg);
    }
};

var g_net_msg_create_server = func ~(in, out, signal, port, log) {
    var r = g_net_msg_create_server_internal(port);
    if (r) {
        if (log) { g_write_pipe_n(out, "Server initializing..."); }
        while (true) {
            var s = g_net_msg_get_server_status();
            if (s == 3) {
                g_write_pipe_n(out, g_net_msg_get_error());
            }
            if (s == 0) {
                return;
            }
            if (s == 2) {
                g_printn("Running msg server, port: " + port);
                if (log) { g_write_pipe_n(out, "Server created successfully!"); }
                break;
            }
        }
        msg_receive(out, signal, port, log);
        shutdown_server(out, signal, port, log);
    } else {
        g_write_pipe_n(out, "Client/Server already created!");
    }
};
export "g_net_msg_create_server";

// --------------------------------------------------------
// CLIENT
var shutdown_client = func ~(out, signal, addr) {
    g_net_msg_shutdown_client();
    g_printn("Shutting down msg client, addr: " + addr);
    g_write_pipe_n(out, "Client stopped!");
    while (true) {
        var s = g_net_msg_get_client_status();
        if (s == 3) {
            g_write_pipe_n(out, g_net_msg_get_error());
        }
        if (s == 0) {
            break;
        }
    }
};

var msg_send = func ~(in, out, signal, addr) {
    g_write_pipe_n(out, "Client connecting...");
    var i = 0;
    while (g_query_share(signal) && i < 2) {
        var s = g_net_msg_get_client_msg();
        if (g_is_null(s)) {
            g_task_sleep_signal(1, signal);
            i++;
            continue;
        }
        g_printdn("Received msg: " + s);
        var obj = g_net_parse_json(s);
        if (g_is_null(obj)) {
            g_task_sleep_signal(1, signal);
            continue;
        }
        var type = obj["type"];
        var addr = obj["addr"];
        var content = obj["content"];
        var msg = "[" + type + "] " + addr + ": " + content;
        g_write_pipe_n(out, "[" + type + "] " + addr + ": " + content);
        g_printdn("Received msg: " + msg);
    }
    var buf = [];
    var pipe = func ["PIPE"] ~(ch, out) {
        if (ch == '\n') {
            var str = g_string_build(buf);
            g_printn("Sending msg: " + str);
            g_net_msg_client_send(str);
            g_array_clear(buf);
        } else {
            g_array_add(buf, ch);
        }
        g_write_pipe(out, ch);
    };
    g_read_pipe_args(in, pipe, out);
};

var g_net_msg_create_client = func ~(in, out, signal, addr) {
    var r = g_net_msg_create_client_internal(addr);
    if (r) {
        g_write_pipe_n(out, "Client initializing...");
        while (true) {
            var s = g_net_msg_get_client_status();
            if (s == 3) {
                g_write_pipe_n(out, g_net_msg_get_error());
            }
            if (s == 0) {
                return;
            }
            if (s == 2) {
                g_printn("Running msg client, addr: " + addr);
                g_write_pipe_n(out, "Client created successfully!");
                break;
            }
        }
        msg_send(in, out, signal, addr);
        shutdown_client(out, signal, addr);
    } else {
        g_write_pipe_n(out, "Client/Server already created!");
    }
};
export "g_net_msg_create_client";

// --------------------------------------------------------
// TASK SSHD
var sshd_shutdown_server = func ~(port) {
    g_net_msg_shutdown_server();
    g_printn("Shutting down msg server, port: " + port);
    while (true) {
        var s = g_net_msg_get_server_status();
        if (s == 3) {
            g_ui_printn(g_net_msg_get_error());
        }
        if (s == 0) {
            break;
        }
    }
};

var task_fast_invoker = func ~(cmd, arg) {
    cmd := g_string_right(cmd, 1);
    g_array_remove(arg, 0);
    if (g_array_size(arg) == 1) {
        return g_task_get_fast(cmd, arg[0]);
    } else if (g_array_size(arg) == 2) {
        return g_task_get_fast_arg(cmd, arg[0], arg[1]);
    } else {
        var msg = arg[0];
        g_array_remove(arg, 0);
        return g_task_get_fast_args(cmd, msg, arg);
    }
};

// TASK FAST INVOKER
var handle_task_command = func ~(arg) {
    var arr = g_string_split(arg, " ");
    var cmd = arr[0];
    if (g_string_start_with(cmd, "@")) {
        return task_fast_invoker(cmd, arr);
    }
};

var sshd_run_lisp = func ~(code) {
    var env = g_lisp_env();
    code := g_string_right(code, 1);
    var val = g_lisp_repl(env, code);
    return val;
};

var sshd_msg_receive = func ~(port) {
    g_ui_printn("Server listening...");
    var cnt = 0;
    g_start_share("SSHD#EXIT", false);
    while (true) {
        if (g_query_share("SSHD#EXIT")) {
            g_stop_share("SSHD#EXIT");
            break;
        }
        if (!g_is_null(g_query_share("CMD#EXIT"))) {
            break;
        }
        var s = g_net_msg_get_server_msg();
        if (g_is_null(s)) {
            g_task_sleep(1);
            continue;
        }
        g_printdn("Received msg: " + s);
        var obj = g_net_parse_json(s);
        if (g_is_null(obj)) {
            g_task_sleep(1);
            continue;
        }
        var type = obj["type"];
        var origin = obj["origin"];
        var addr = obj["addr"];
        var content = obj["content"];
        var msg = "[" + type + "] " + addr + ": " + content;
        g_ui_printn("[" + type + "] " + addr + ": " + content);
        g_printdn("Received msg: " + msg);
        g_printn("*** Remote: " + content);
        if (type == "INFO" && g_string_start_with(content, "Hello")) {
            cnt++;
            continue;
        }
        if (type == "INFO" && g_string_start_with(content, "Bye")) {
            cnt--;
            //if (cnt == 0) { break; }
        }

        if (g_string_start_with(content, "(")) {
            g_net_msg_server_send_with_origin(sshd_run_lisp(content), origin);
        } else if (g_string_start_with(content, "@")) {
            var ret = handle_task_command(content);
            if (g_is_null(ret)) {
                g_net_msg_server_send_with_origin("Error: invalid command", origin);
            } else {
                g_net_msg_server_send_with_origin(g_to_string(ret), origin);
            }
        } else {
            import "sys.math";
            import "sys.class";
            import "std.base";
            import "std.shell";
            var ctx = g_create_context();
            g_import_std_base();
            g_import_std_shell(ctx);
            var shell = g_create_class(ctx, "system::shell");
            var out = g_create_pipe("SSHD#BASH.OUT");
            var buf = [];
            var out_proc = func ~(buf) {
                var o = g_create_pipe("SSHD#BASH.OUT");
                g_read_pipe(o, lambda(ch) {
                    g_ui_print(ch);
                    g_array_add(buf, ch);
                });
            };
            var out_pid = g_create_user_process_args(out_proc, buf);
            set shell::"out" = out;
            shell."exec"(content);
            var str = g_string_build(buf);
            g_destroy_pipe(out);
            g_net_msg_server_send_with_origin(str, origin);
            g_join_process(out_pid);
        }
    }
};

var g_net_sshd = func ~(port) {
    port := g_string_atoi(port);
    if (port == -1) {
        return "Error: Invalid port";
    }
    var r = g_net_msg_create_server_internal(port);
    if (r) {
        g_ui_printn("Server initializing...");
        while (true) {
            var s = g_net_msg_get_server_status();
            if (s == 3) {
                g_ui_printn(g_net_msg_get_error());
            }
            if (s == 0) {
                return "Error";
            }
            if (s == 2) {
                g_printn("Running msg server, port: " + port);
                g_ui_printn("Server created successfully!");
                break;
            }
        }
        sshd_msg_receive(port);
        sshd_shutdown_server(port);
        return "Server stopped!";
    } else {
        return "Error: Client/Server already created!";
    }
};
export "g_net_sshd";

// TASK SSHD
// --------------------------------------------------------
// TASK SSH
var ssh_shutdown_client = func ~(addr) {
    g_net_msg_shutdown_client();
    g_printn("Shutting down msg client, addr: " + addr);
    while (true) {
        var s = g_net_msg_get_client_status();
        if (s == 3) {
            g_ui_printn(g_net_msg_get_error());
        }
        if (s == 0) {
            break;
        }
    }
};

var ssh_msg_recv = func ~(address) {
    var i = 0;
    while (i < 2) {
        // RECV
        var s = g_net_msg_get_client_msg();
        if (g_is_null(s)) {
            g_task_sleep(1);
            i++;
            continue;
        }
        g_printdn("Received msg: " + s);
        var obj = g_net_parse_json(s);
        if (g_is_null(obj)) {
            g_task_sleep(1);
            i++;
            continue;
        }
        var origin = obj["origin"];
        var type = obj["type"];
        var addr = obj["addr"];
        var content = obj["content"];
        var msg = "[" + type + "] " + addr + ": " + content;
        if (type == "ECHO") {
            if (!g_is_null(origin) && g_net_msg_get_client_addr() == origin) {
                g_ui_printn(content);
            }
        } else if (type == "ERR ") {
            g_ui_printn(msg);
            return false;
        } else {
            if (!g_is_null(origin) && g_array_empty(address)) {
                g_array_add(address, origin);
            }
            //g_ui_printn("[" + type + "] " + addr + ": " + content);
        }
        g_printdn("Received msg: " + msg);
    }
    return true;
};

var ssh_msg_send = func ~() {
    g_ui_printn("Client connecting...");
    var address = [];
    while (true) {
        // RECV
        if (!ssh_msg_recv(address)) {
            break;
        }

        g_ui_print("# \uffef");
        g_sleep(400);

        // SEND
        var cmd = g_ui_input();
        cmd := g_string_trim(cmd);
        if (cmd == "exit") { break; }
        if (g_string_length(cmd) > 0) {
            if (g_array_empty(address)) {
                g_net_msg_client_send(cmd);
            } else {
                g_net_msg_client_send_with_origin(cmd, address[0]);
            }
        }
    }
};

var g_net_ssh = func ~(addr) {
    var r = g_net_msg_create_client_internal(addr);
    if (r) {
        g_ui_printn("Client initializing...");
        while (true) {
            var s = g_net_msg_get_client_status();
            if (s == 3) {
                g_ui_printn(g_net_msg_get_error());
            }
            if (s == 0) {
                return "Error";
            }
            if (s == 2) {
                g_printn("Running msg client, addr: " + addr);
                g_ui_printn("Client created successfully!");
                break;
            }
        }
        ssh_msg_send();
        ssh_shutdown_client(addr);
        return "Client stopped!";
    } else {
        return "Error: Client/Server already created!";
    }
};
export "g_net_ssh";

// TASK SSH
// --------------------------------------------------------